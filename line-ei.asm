		; üêü —Ä–∏—Å–æ–≤–∞–Ω–∏—è –ª–∏–Ω–∏–∏ –∞–ª–≥–æ—Ä–∏—Ç–º–æ–º –ë—Ä–µ–∑–µ–Ω—Ö–µ–º–∞ –≤ —Ä–µ–∂–∏–º–µ 256—Ö256
		; –¢–µ—Å—Ç –∏ –±–µ–Ω—á–º–∞—Ä–∫
		; 
		; –î–ª—è –∑–∞–ø—É—Å–∫–∞ –±–µ–Ω—á–º–∞—Ä–∫–∞ –Ω–∞–∂–∞—Ç—å –£–° / –°–° / –†–£–°–õ–ê–¢
                ; (–≤ —Å—Ä–µ–¥–Ω–µ–º 257 –ª–∏–∏–Ω–π –≤ —Å–µ–∫—É–Ω–¥—É)
		;
		; –¢–æ—á–∫–∞ –≤—Ö–æ–¥–∞ –ø—Ä–æ—Ü–µ–¥—É—Ä—ã —Ä–∏—Å–æ–≤–∞–Ω–∏—è: 
		; 	line
		; –í—Ö–æ–¥–Ω—ã–µ –ø–∞—Ä–∞–º–µ—Ç—Ä—ã:
		; 	line_x0, line_y0, line_x1, line_y1
		; –†–∏—Å–æ–≤–∞–Ω–∏–µ –ø—Ä–æ–∏—Å—Ö–æ–¥–∏—Ç –≤ –ø–ª–æ—Å–∫–æ—Å—Ç–∏ 0x8000
		;
		; –í—è—á–µ—Å–ª–∞–≤ –°–ª–∞–≤–∏–Ω—Å–∫–∏–π –∏ –ò–≤–∞–Ω –ì–æ—Ä–æ–¥–µ—Ü–∫–∏–π, 2017
		; –≤–µ—Ä—Å–∏—è 25.1

                .binfile line-ei.rom
                .tapfile line-ei.tap v06c-rom
		.org 100h

		di
; ---
start:
		xra	a
		out	10h
		lxi	sp,100h
		mvi	a,0C3h
		sta	0
		lxi	h,Restart
		shld	1

		call	Cls
		mvi	a,0C9h
		sta	38h
		ei
		hlt
		lxi	h, colors+15
colorset:
		mvi	a, 88h
		out	0
		mvi	c, 15
colorset1:	mov	a, c
		out	2
		mov	a, m
		out	0Ch
		dcx	h
		out	0Ch
		out	0Ch
		dcr	c
		out	0Ch
		out	0Ch
		out	0Ch
		jp	colorset1
		mvi	a,255
		out	3


Restart:
		call	Cls

		call SetPixelModeOR
		
;—Ä–∞–º–∫–∞ –ø–æ –∫—Ä–∞—é		
		lxi h,00000h
		shld line_x0
		lxi h,00FFh
		shld line_x1
		call line
		lxi h,00000h
		shld line_x0
		lxi h,0FF00h
		shld line_x1
		call line
		
		lxi h,0FFFFh
		shld line_x0
		lxi h,00FFh
		shld line_x1
		call line
		lxi h,0FFFFh
		shld line_x0
		lxi h,0FF00h
		shld line_x1
		call line

;–≤–Ω—É—Ç—Ä–µ–Ω—è—è —Ä–∞–º–∫–∞
		lxi h,01010h
		shld line_x0
		lxi h,010F0h
		shld line_x1
		call line
		lxi h,01010h
		shld line_x0
		lxi h,0F010h
		shld line_x1
		call line

		lxi h,0F0F0h
		shld line_x0
		lxi h,0F010h
		shld line_x1
		call line
		lxi h,0F0F0h
		shld line_x0
		lxi h,010F0h
		shld line_x1
		call line

;—É–≥–æ–ª–∫–∏		
		lxi h,00808h
		shld line_x0
		lxi h,00908h
		shld line_x1
		call line
		lxi h,00808h
		shld line_x0
		lxi h,00809h
		shld line_x1
		call line

		lxi h,0F8F8h
		shld line_x0
		lxi h,0F8F7h
		shld line_x1
		call line
		lxi h,0F8F8h
		shld line_x0
		lxi h,0F7F8h
		shld line_x1
		call line
		
		lxi h,008F8h
		shld line_x0
		lxi h,009F8h
		shld line_x1
		call line
		lxi h,008F8h
		shld line_x0
		lxi h,008F7h
		shld line_x1
		call line

		lxi h,0F808h
		shld line_x0
		lxi h,0F809h
		shld line_x1
		call line
		lxi h,0F808h
		shld line_x0
		lxi h,0F708h
		shld line_x1
		call line

;—Ç–æ—á–∫–∏
		lxi h,02020h
		shld line_x0
		shld line_x1
		call line

		lxi h,0DF20h
		shld line_x0
		shld line_x1
		call line

		lxi h,020DFh
		shld line_x0
		shld line_x1
		call line

		lxi h,0DFDFh
		shld line_x0
		shld line_x1
		call line
		
		lxi h,circl
circloop:
		push h
		lxi h,8080h
		shld line_x0
		pop h
		mov a,m
		ora a
		jz benchmark
		sta line_x1
		inx h
		mov a,m
		sta line_y1
		inx h
		push h
		call line
		pop h
		jmp circloop

circl:
		.db 228,128
		.db 220,166
		.db 199,199
		.db 166,220
		.db 128,228
		.db 90,220
		.db 57,199
		.db 36,166
		.db 28,128
		.db 36,90
		.db 57,57
		.db 90,36
		.db 128,28
		.db 166,36
		.db 199,57
		.db 220,90
		.db 0

benchmark
		in 1
		rlc
		jnc benchmark_go
		rlc
		jnc benchmark_go
		rlc
		jc benchmark

benchmark_go
		call SetPixelModeXOR

		call rnd16
		shld line_tail
foreva:
		lhld line_tail
		shld line_x0

		call rnd16
		mov d, h
		mov e, l
		inx d
		mov a, d
		ora e
		jz foreva_nomoar
		shld line_x1
		shld line_tail

		call line
		jmp foreva
foreva_nomoar
		jmp Restart

line_tail:	.dw 0

		; –∞—Ä–≥—É–º–µ–Ω—Ç—ã line()
line_x0		.db 100
line_y0		.db 55
line_x1		.db 0
line_y1		.db 50 

		; —ç—Ç–∏ —á–µ—Ç—ã—Ä–µ –±–∞–π—Ç–∞ –¥–æ–ª–∂–Ω—ã –∏–¥—Ç–∏ –≤ —Ç–∞–∫–æ–º –ø–æ—Ä—è–¥–∫–µ, –∞ —Ç–æ
line_y		.db 0
line_x		.db 0
line_dx 	.db 0
line_dy		.db 0

SetPixelModeXOR:
		lxi h,0A9AEh		;A9 - xra c; AE - xra m
		jmp SetPixelModeOR1
		
SetPixelModeOR:
		lxi h,0B1B6h		;B1 - ora c; B6 - ora m
SetPixelModeOR1:
		mov a,l
		sta SetPixelMode_g3
		sta SetPixelMode_g4
		sta SetPixelMode_s3
		mov a,h
		sta SetPixelMode_g1
		sta SetPixelMode_g2
		sta SetPixelMode_s1
		sta SetPixelMode_s2
		ret
		
PixelMask:
		.db 10000000b
		.db 01000000b
		.db 00100000b
		.db 00010000b
		.db 00001000b
		.db 00000100b
		.db 00000010b
		.db 00000001b
line:		; –≤—ã—á–∏—Å–ª–∏—Ç—å line_dx, line_dy –∏ –ø—Ä–∏—Ä–∞—â–µ–Ω–∏–µ Y
		; line_dx >= 0, line_dy >= 0, line1_mod_yinc ? [-1,1]

		; –≤—ã—á–∏—Å–ª–µ–Ω–∏–µ —Ä–∞—Å—Å—Ç–æ—è–Ω–∏—è –ø–æ X (dx)
		; –ø—Ä–æ–≤–µ—Ä–∏—Ç—å, —á—Ç–æ x0 <= x1
		lda line_x0
		sta line_x
		mov b, a		;b = x0
		lda line_x1
		sub b			;a = x1 - x0
		jnc line_x_positive     ;–µ—Å–ª–∏ x0 <= x1, —Ç–æ –ø–µ—Ä–µ—Ö–æ–¥

		;–µ—Å–ª–∏ x0 > x1, —Ç–æ –ø—Ä–∏—à–ª–∏ —Å—é–¥–∞
		cma
		inr a			; -(x1-x0)=x0-x1
		sta line_dx		; —Å–æ—Ö—Ä–∞–Ω–∏–ª–∏ |dx|
		lhld line_x0
		xchg
		lhld line_x1
		shld line_x0
		mov a,l
		sta line_x
		xchg
		shld line_x1
		jmp line_calc_dy
		
line_x_positive:
		sta line_dx		; —Å–æ—Ö—Ä–∞–Ω–∏–ª–∏ |dx|

		; –≤—ã—á–∏—Å–ª–µ–Ω–∏–µ —Ä–∞—Å—Å—Ç–æ—è–Ω–∏—è –ø–æ Y (dy)
line_calc_dy:
		; –µ—Å–ª–∏ y0 <= y1
		lda line_y0
		sta line_y
		mov b, a		;b = y0
		lda line_y1
		sub b			;a = y1 - y0
		jnc line_y_positive	;–µ—Å–ª–∏ y0 <= y1, —Ç–æ –ø–µ—Ä–µ—Ö–æ–¥

		;–µ—Å–ª–∏ y0 > y1, —Ç–æ –ø—Ä–∏—à–ª–∏ —Å—é–¥–∞
		cma
		inr a			; -(y1-y0)= y0 - y1
		sta line_dy		; —Å–æ—Ö—Ä–∞–Ω–∏–ª–∏ |dy|
		
		; –ø—Ä–∏—Ä–∞—â–µ–Ω–∏–µ y = -1
		mvi a, 02Dh 		; dcr l
		jmp set_line1_mod_yinc

line_y_positive:
		sta line_dy	        ; y1 - y0
		mvi a, 02Ch 		; inr l
set_line1_mod_yinc:
		sta line1_mod_yinc_g
		sta line1_mod_yinc_s1
		sta line1_mod_yinc_s2

line_check_gs:
		; –ø—Ä–æ–≤–µ—Ä—è–µ–º –∫—Ä—É—Ç–∏–∑–Ω—É —Å–∫–ª–æ–Ω–∞:
		; dy >= 0, dx >= 0
		;  	dy <= dx 	?	–ø–æ–ª–æ–≥–∏–π
		;	dy > dx 	?	–∫—Ä—É—Ç–æ–π
		lhld line_dx 	                ; l = dx, h = dy
		mov a, l 
		cmp h				;–µ—Å–ª–∏ dy<=dx
		jnc  line_gentle	        ;—Ç–æ —Å–∫–ª–æ–Ω –ø–æ–ª–æ–≥–∏–π
		
		; –∫—Ä—É—Ç–æ–π —Å–∫–ª–æ–Ω
		; –Ω–∞—á–∞–ª—å–Ω–æ–µ –∑–Ω–∞—á–µ–Ω–∏–µ D
		; D = 2 * dx - dy
		lda line_dy
		cma
		mov e,a
		mvi d,0FFh
		inx d				; de = -dy

		lhld line_dx
		mvi h,0
		dad h
		shld line1_mod_dx_s+1		; —Å–æ—Ö—Ä–∞–Ω–∏—Ç—å 2*dx –∫–æ–Ω—Å—Ç–∞–Ω—Ç–æ–π
		; —Å–æ—Ö—Ä–∞–Ω–∏—Ç—å 2*dx –∫–æ–Ω—Å—Ç–∞–Ω—Ç–æ–π
		mov a,l
		sta line1_mod_dx_sLo+1		; —Å–æ—Ö—Ä–∞–Ω–∏—Ç—å 2*dx –∫–æ–Ω—Å—Ç–∞–Ω—Ç–æ–π
		mov a,h
		sta line1_mod_dx_sHi+1		; —Å–æ—Ö—Ä–∞–Ω–∏—Ç—å 2*dx –∫–æ–Ω—Å—Ç–∞–Ω—Ç–æ–π

		dad d				; hl = 2 * dx - dy
		push h				; –ø–æ–º–µ—Å—Ç–∏—Ç—å –≤ —Å—Ç–µ–∫ –∑–Ω–∞—á–µ–Ω–∏–µ D = 2 * dx - dy
		xchg				; hl = -dy
		
		dad h				; hl = -2*dy
line1_mod_dx_s:
		lxi d,0				; de = 2*dx
		dad d 				; hl = 2 * dx - 2 * dy
		; —Å–æ—Ö—Ä–∞–Ω–∏—Ç—å –∫–∞–∫ –∫–æ–Ω—Å—Ç
		mov a,l
		sta line1_mod_dxdy_sLo+1
		mov a,h
		sta line1_mod_dxdy_sHi+1

		lhld line_y	;h=x; l=y
		xchg		;d=x; e=y
		mvi a,111b
		ana d
		adi PixelMask&255
		mov l,a
		mvi a,PixelMask>>8
		aci 0
		mov h,a			; hl - –∞–¥—Ä–µ—Å –º–∞—Å–∫–∏ –≤ PixelMask
		mov c,m 		; –Ω–∞—á–∞–ª—å–Ω–æ–µ –∑–Ω–∞—á–µ–Ω–∏–µ –º–∞—Å–∫–∏ –ø–∏–∫—Å–µ–ª—è
		
		mvi a,11111000b
		ana d
		rrc
		rrc 
		stc 
		rar 
		xchg 		        ; l=y
		mov h,a		        ; h=—Å—Ç–∞—Ä—à–∏–π –±–∞–π—Ç —ç–∫—Ä–∞–Ω–Ω–æ–≥–æ –∞–¥—Ä–µ—Å–∞
		pop d			; de = 2 * dx - dy

		lda line_dy
		mov b,a

		;------ –∫—Ä—É—Ç–æ–π —Ü–∏–∫–ª (s/steep) -----
line1_loop_s:	; <--- —Ç–æ—á–∫–∞ –≤—Ö–æ–¥–∞ –≤ –∫—Ä—É—Ç–æ–π —Ü–∏–∫–ª --->
		mov a,m
SetPixelMode_s1:
		xra c
		mov m,a	 		; –∑–∞–ø–∏—Å–∞—Ç—å –≤ –ø–∞–º—è—Ç—å —Ä–µ–∑—É–ª—å—Ç–∞—Ç —Å –∏–∑–º–µ–Ω–µ–Ω–Ω—ã–º –ø–∏–∫—Å–µ–ª–µ–º

		; if D > 0
		xra a
		ora d
		jp line1_then_s
line1_else_s: 	; else –æ—Ç if D > 0
line1_mod_yinc_s2:
		inr l			; y = y +/- 1
		mov a,m
SetPixelMode_s2:
		xra c
		mov m,a	 		; –∑–∞–ø–∏—Å–∞—Ç—å –≤ –ø–∞–º—è—Ç—å —Ä–µ–∑—É–ª—å—Ç–∞—Ç —Å –∏–∑–º–µ–Ω–µ–Ω–Ω—ã–º –ø–∏–∫—Å–µ–ª–µ–º
		dcr b
		rz
line1_mod_dx_sLo:
		mvi a,0		        ; –∏–∑–º–µ–Ω—è–µ–º—ã–π –∫–æ–¥ (2*dx) –º–ª–∞–¥—à–∏–π –±–∞–π—Ç
		add e
		mov e,a
line1_mod_dx_sHi:
		mvi a,0		        ; –∏–∑–º–µ–Ω—è–µ–º—ã–π –∫–æ–¥ (2*dx) —Å—Ç–∞—Ä—à–∏–π –±–∞–π—Ç
		adc d
		mov d,a
		;–≤ –∏—Ç–æ–≥–µ de = de + 2*dx
		jm line1_else_s

line1_then_s:
line1_mod_yinc_s1:
		inr l			; y = y +/- 1
		mov a,c
		rrc 			; xincLo
		mov c,a
		jnc $+4
		inr h			; xincHi
SetPixelMode_s3:
		xra m
		mov m,a	 		; –∑–∞–ø–∏—Å–∞—Ç—å –≤ –ø–∞–º—è—Ç—å —Ä–µ–∑—É–ª—å—Ç–∞—Ç —Å –∏–∑–º–µ–Ω–µ–Ω–Ω—ã–º –ø–∏–∫—Å–µ–ª–µ–º
		dcr b
		rz
line1_mod_dxdy_sLo:
		mvi a,0			; –∏–∑–º–µ–Ω—è–µ–º—ã–π –∫–æ–¥: 2*(dx-dy) –º–ª–∞–¥—à–∏–π –±–∞–π—Ç
		add e
		mov e,a
line1_mod_dxdy_sHi:
		mvi a,0			; –∏–∑–º–µ–Ω—è–µ–º—ã–π –∫–æ–¥: 2*(dx-dy) —Å—Ç–∞—Ä—à–∏–π –±–∞–π—Ç
		adc d
		mov d,a
		;–≤ –∏—Ç–æ–≥–µ de = de + 2*(dx-dy)
		jm line1_else_s
		jmp line1_then_s
		; --- –∫–æ–Ω–µ—Ü —Ç–µ–ª–∞ –∫—Ä—É—Ç–æ–≥–æ —Ü–∏–∫–ª–∞ ---

		
line_gentle:
		; —Å–∫–ª–æ–Ω –ø–æ–ª–æ–≥–∏–π
		; –Ω–∞—á–∞–ª—å–Ω–æ–µ –∑–Ω–∞—á–µ–Ω–∏–µ D
		; D = 2 * dy - dx
		lda line_dx
		cma
		mov e,a
		mvi d,0FFh
		inx d				; de = -dx

		lhld line_dy
		mvi h,0
		dad h
		shld line1_mod_dy_g+1		; —Å–æ—Ö—Ä–∞–Ω–∏—Ç—å 2*dy –∫–æ–Ω—Å—Ç–∞–Ω—Ç–æ–π
		; —Å–æ—Ö—Ä–∞–Ω–∏—Ç—å 2*dy –∫–æ–Ω—Å—Ç–∞–Ω—Ç–æ–π
		mov a,l
		sta line1_mod_dy_gLo+1
		mov a,h
		sta line1_mod_dy_gHi+1


		dad d				; hl = 2 * dy - dx
		push h				; –ø–æ–º–µ—Å—Ç–∏—Ç—å –≤ —Å—Ç–µ–∫ –∑–Ω–∞—á–µ–Ω–∏–µ D = 2 * dy - dx
		xchg				; hl = -dx
		
		dad h				; hl = -2*dx
line1_mod_dy_g:
		lxi d,0
		dad d 				; hl = 2 * dy - 2 * dx
		mov a,l
		sta line1_mod_dydx_gLo+1        ; —Å–æ—Ö—Ä–∞–Ω–∏—Ç—å –∫–∞–∫ –∫–æ–Ω—Å—Ç
		mov a,h
		sta line1_mod_dydx_gHi+1        ; —Å–æ—Ö—Ä–∞–Ω–∏—Ç—å –∫–∞–∫ –∫–æ–Ω—Å—Ç
		
		pop d				; de = 2 * dy - dx

		; –æ—Å–Ω–æ–≤–Ω–æ–π —Ü–∏–∫–ª —Ä–∏—Å–æ–≤–∞–Ω–∏—è –ª–∏–Ω–∏–∏
		; –≤–µ—Ä—Å–∏—è –¥–ª—è –ø–æ–ª–æ–≥–æ–≥–æ —Å–∫–ª–æ–Ω–∞ (_g)
		lhld line_x	;l=x h=dx
		mov c,l		;c=x
		mov b,h		;line_dx
		lda line_y	;a=y
		sta line_yx_g+1

		; –ø–æ–¥–≥–æ—Ç–æ–≤–∏—Ç—å –Ω–∞—á–∞–ª—å–Ω–æ–µ –∑–Ω–∞—á–µ–Ω–∏–µ —Ä–µ–≥–∏—Å—Ç—Ä–∞ c
		mvi a, 111b 		; —Å–Ω–∞—á–∞–ª–∞ –≤—ã—á–∏—Å–ª—è–µ–º —Å–º–µ—â–µ–Ω–∏–µ 
		ana c 			; –ø–∏–∫—Å–µ–ª—è –≤ PixelMask (—Å = x)
		adi PixelMask&255
		mov l,a
		mvi a,PixelMask>>8
		aci 0
		mov h,a			; hl - –∞–¥—Ä–µ—Å –º–∞—Å–∫–∏ –≤ PixelMask
		mvi a,11111000b
		ana c
		rrc
		rrc
		stc
		rar
		sta line_yx_g+2	; 0x80 | (x >> 3), l = y

		xra a
		cmp b		        ;dx=0?
		mov c,m			; –º–∞—Å–∫–∞
line_yx_g:
		lxi h, 0                ; hl —É–∫–∞–∑—ã–≤–∞–µ—Ç –≤ —ç–∫—Ä–∞–Ω
		jnz line1_loop_g	;–µ—Å–ª–∏ dx<>0, —Ç–æ –ø–µ—Ä–µ—Ö–æ–¥ –Ω–∞ –æ–±—ã—á–Ω–æ–µ —Ä–∏—Å–æ–≤–∞–Ω–∏–µ –ª–∏–Ω–∏–∏
;–µ—Å–ª–∏ dx=0, —Ç–æ —Å—Ç–∞–≤–∏–º –æ–¥–Ω—É —Ç–æ—á–∫—É
                mov a,m
SetPixelMode_g2:
		xra c
		mov m,a 		; –∑–∞–ø–∏—Å–∞—Ç—å –≤ –ø–∞–º—è—Ç—å
		ret

		;------ –ø–æ–ª–æ–≥–∏–π —Ü–∏–∫–ª (g/gentle) -----
line1_loop_g:	; <--- —Ç–æ—á–∫–∞ –≤—Ö–æ–¥–∞ –≤ –ø–æ–ª–æ–≥–∏–π —Ü–∏–∫–ª --->
		mov a,m
SetPixelMode_g1:
		xra c
		mov m,a 		; –∑–∞–ø–∏—Å–∞—Ç—å –≤ –ø–∞–º—è—Ç—å

		; if D > 0
		xra a
		ora d
		jp line1_then_g
line1_else_g: 	; else –æ—Ç if D > 0
		mov a,c
		rrc 			; —Å–¥–≤–∏–Ω—É—Ç—å –≤–ø—Ä–∞–≤–æ (—Å–ª–µ–¥—É—é—â–∏–π X)
		mov c,a			; —Å–æ—Ö—Ä–∞–Ω—è–µ–º —Ç–µ–∫—É—â–µ–µ –∑–Ω–∞—á–µ–Ω–∏–µ –º–∞—Å–∫–∏
		jnc $+4 		; –µ—Å–ª–∏ –Ω–µ –ø—Ä–æ–≤–µ—Ä–Ω—É–ª—Å—è —á–µ—Ä–µ–∑ –∫—Ä–∞–π
		inr h			;line_x += 1
SetPixelMode_g3:
		xra m
		mov m,a 		; –∑–∞–ø–∏—Å–∞—Ç—å –≤ –ø–∞–º—è—Ç—å
		dcr b			; dx -= 1
		rz

line1_mod_dy_gLo:
		mvi a,0		        ; –∏–∑–º–µ–Ω—è–µ–º—ã–π –∫–æ–¥ (2*dy) –º–ª–∞–¥—à–∏–π –±–∞–π—Ç
		add e
		mov e,a
line1_mod_dy_gHi:
		mvi a,0		        ; –∏–∑–º–µ–Ω—è–µ–º—ã–π –∫–æ–¥ (2*dy) —Å—Ç–∞—Ä—à–∏–π –±–∞–π—Ç
		adc d
		mov d,a
		;–≤ –∏—Ç–æ–≥–µ de= de + 2*dy
		jm line1_else_g

line1_then_g:
line1_mod_yinc_g:
		inr l			; –∏–∑–º–µ–Ω—è–µ–º—ã–π –∫–æ–¥: line_y += yinc –∏–ª–∏ line_y -= yinc
		mov a,c
		rrc 			; —Å–¥–≤–∏–Ω—É—Ç—å –≤–ø—Ä–∞–≤–æ (—Å–ª–µ–¥—É—é—â–∏–π X)
		mov c,a			; —Å–æ—Ö—Ä–∞–Ω—è–µ–º —Ç–µ–∫—É—â–µ–µ –∑–Ω–∞—á–µ–Ω–∏–µ –º–∞—Å–∫–∏
		jnc $+4 		; –µ—Å–ª–∏ –Ω–µ –ø—Ä–æ–≤–µ—Ä–Ω—É–ª—Å—è —á–µ—Ä–µ–∑ –∫—Ä–∞–π
		inr h			;line_x += 1
SetPixelMode_g4:
		xra m
		mov m,a 		; –∑–∞–ø–∏—Å–∞—Ç—å –≤ –ø–∞–º—è—Ç—å
		dcr b			; dx -= 1
		rz
line1_mod_dydx_gLo:
		mvi a,0		        ; –∏–∑–º–µ–Ω—è–µ–º—ã–π –∫–æ–¥: 2*(dy-dx) –º–ª–∞–¥—à–∏–π –±–∞–π—Ç
		add e
		mov e,a
line1_mod_dydx_gHi:
		mvi a,0		        ; –∏–∑–º–µ–Ω—è–µ–º—ã–π –∫–æ–¥: 2*(dy-dx) —Å—Ç–∞—Ä—à–∏–π –±–∞–π—Ç
		adc d
		mov d,a
		;–≤ –∏—Ç–æ–≥–µ de = de + 2*(dy-dx)
		jm line1_else_g
		jmp line1_then_g
		; --- –∫–æ–Ω–µ—Ü —Ç–µ–ª–∞ –ø–æ–ª–æ–≥–æ–≥–æ —Ü–∏–∫–ª–∞ ---

		; --- –∫–æ–Ω–µ—Ü line() ---
		
Cls:
		lxi	h,08000h
		mvi	e,0
		xra	a
ClrScr:
		mov	m,e
		inx	h
		cmp	h
		jnz	ClrScr
		ret

		; –≤—ã—Ö–æ–¥:
		; HL - —á–∏—Å–ª–æ –æ—Ç 1 –¥–æ 65535
rnd16:
		lxi h,65535
		dad h
		shld rnd16+1
		rnc
		mvi a,00000001b ;–ø–µ—Ä–µ–≤–µ—Ä–Ω—É–ª 80h - 10000000b
		xra l
		mov l,a
		mvi a,01101000b	;–ø–µ—Ä–µ–≤–µ—Ä–Ω—É–ª 16h - 00010110b
		xra h
		mov h,a
		shld rnd16+1
		ret

colors:
		.db 00000000b,00001001b,00010010b,00011011b,00100100b,00101101b,00110110b,00111111b
		.db 11111111b,00001001b,00010010b,00011011b,00100100b,00101101b,00110110b,00111111b

		.end
